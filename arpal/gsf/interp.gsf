#-- interpola linearmente tra due valori di dimensioni
#-- uso: per trovare il campo a T=1.3, o X=7.6
function interp(var, dim, dimval)

#-- controlli
 if(dim='' | dim='dim');dimval='help';endif
 if(var='' | var='var' | var='help')
  say 'uso: interpVal(var,dim,dimval)'
  say 'ex.: interpVal(mslp/100,t,1.5) torna la interpolata lineare tra t=1 e t=2'
  say 'dim valide: x y z t e lon lat lev time'
  return
 endif

#-- salvo il setting delle dimensioni 
 dummy=dimens()                                                                                                                                                                              
 x1=subwrd(dummy,1)
 x2=subwrd(dummy,2)
 y1=subwrd(dummy,3)
 y2=subwrd(dummy,4)
 z1=subwrd(dummy,5)
 z2=subwrd(dummy,6)
 t1=subwrd(dummy,7)
 t2=subwrd(dummy,8)
* 'q dims';say result

#-- se passo una dimensione globale (lon lat lev time) la trasformo in una di griglia (x y z t)
#-- per ora implementata solo la time
 if(dim='lon' | dim='lat'| dim='lev'| dim='time')
  'set t 1'
  'set x 1'
  'set y 1'
  'set z 1'
  'set e 1'
  if(dim='time')
   'set t 1 last'
   'd lon'
*   'q dims';say result
*   say 'q w2gr 'dimval' 1'
   'q w2gr 'dimval' 1'
*   say result
   dimval=subwrd(result,3)
   dim='t'
  else
   say 'uso: interpVal(var,dim,dimval) ... al momento le dim implementate sono solo: x y z t e time'
   return
  endif
#-- ripristino il setting delle dimensioni precedenti
  if(x1=x2);'set x 'x1;else;'set x 'x1' 'x2;endif
  if(y1=y2);'set y 'y1;else;'set y 'y1' 'y2;endif
  if(z1=z2);'set z 'z1;else;'set z 'z1' 'z2;endif
  if(t1=t2);'set t 't1;else;'set t 't1' 't2;endif
 endif

*say 'interp('var', 'dim', 'dimval')'

#-- verifico che dimval sia un numero
 isNumber=valnum(dimval)
 if(isNumber=0);
  say 'uso: interpVal(var,dim,dimval) ... dimval deve essere un numero'
 endif
#-- caso dimensione intera (non interpolo) ad esempio 2 e 2.0 sono interi
 intval=math_int(dimval)
 if(intval=dimval)
  'set 'dim' 'dimval
  'define gsudf='var
 else
#-- caso dimensione con decimali (interpolo)
  delta=dimval-intval
  'set 'dim' 'intval
  'define gsudf='var
  'set 'dim' '%(intval+1);'q time'
  'define gsudf=gsudf*(1-'delta')+'var'*'delta
 endif

#-- ripristino il setting delle dimensioni precedenti
 if(x1=x2);'set x 'x1;else;'set x 'x1' 'x2;endif
 if(y1=y2);'set y 'y1;else;'set y 'y1' 'y2;endif
 if(z1=z2);'set z 'z1;else;'set z 'z1' 'z2;endif
 if(t1=t2);'set t 't1;else;'set t 't1' 't2;endif
* 'q dims';say result

#-- ritorno il campo richiesto
return gsudf


***************************************************************************
* FUNCTION DIMENS ritorna le dims: x1,x2,y1,y2,z1,z2,t1,t2,lon1,lon2,lat1,lat2,lev1,lev2,tstr1,tstr2
function dimens()

 'query dims'
 linex=sublin(result,2)
 liney=sublin(result,3)
 linez=sublin(result,4)
 linet=sublin(result,5)

 flagx=subwrd(linex,3)
 if(flagx="varying")
  x1=subwrd(linex,11)
  x2=subwrd(linex,13)
  lon1=subwrd(linex,6)
  lon2=subwrd(linex,8)
 else
  x1=subwrd(linex,9)
  x2=subwrd(linex,9)
  lon1=subwrd(linex,6)
  lon2=subwrd(linex,6)
 endif

 flagy=subwrd(liney,3)
 if(flagy="varying")
  y1=subwrd(liney,11)
  y2=subwrd(liney,13)
  lat1=subwrd(liney,6)
  lat2=subwrd(liney,8)
 else
  y1=subwrd(liney,9)
  y2=subwrd(liney,9)
  lat1=subwrd(liney,6)
  lat2=subwrd(liney,6)
 endif

 flagz=subwrd(linez,3)
 if(flagz="varying")
  z1=subwrd(linez,11)
  z2=subwrd(linez,13)
  lev1=subwrd(linez,6)
  lev2=subwrd(linez,8)
 else
  z1=subwrd(linez,9)
  z2=subwrd(linez,9)
  lev1=subwrd(linez,6)
  lev2=subwrd(linez,6)
 endif

 flagt=subwrd(linet,3)
 if(flagt="varying")
  t1=subwrd(linet,11)
  t2=subwrd(linet,13)
  tstr1=subwrd(linet,6)
  ltstr=subwrd(linet,8)
 else
  t1=subwrd(linet,9)
  t2=subwrd(linet,9)
  tstr1=subwrd(linet,6)
  tstr2=subwrd(linet,6)
 endif

return (x1' 'x2' 'y1' 'y2' 'z1' 'z2' 't1' 't2' 'lon1' 'lon2' 'lat1' 'lat2' 'lev1' 'lev2' 'tstr1' 'tstr2)
